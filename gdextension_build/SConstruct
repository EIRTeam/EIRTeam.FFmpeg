#!/usr/bin/env python
import atexit
import os
import sys
import methods
import time
import ffmpeg_download
import glsl_builders

# For the reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags

time_at_start = time.time()

env = SConscript("./godot-cpp/SConstruct")
env.__class__.disable_warnings = methods.disable_warnings
env.__class__.Run = methods.Run

GLSL_BUILDERS = {
    "GLSL_HEADER": env.Builder(
        action=env.Run(glsl_builders.build_raw_headers, 'Building GLSL header: "$TARGET"'),
        suffix="glsl.gen.h",
        src_suffix=".glsl",
    ),
}
env.Append(BUILDERS=GLSL_BUILDERS)

# Allow generation of compilation DB (`compile_commands.json`) for intellisense / code hinting
# Generating the compilation DB requires SCons 4.0.0 or later.
from SCons import __version__ as scons_raw_version

scons_ver = env._get_major_minor_revision(scons_raw_version)

if scons_ver < (4, 0, 0):
    print(
        "Warning: The `compiledb` option requires SCons 4.0 or later, but your version is %s. `compiledb` is not enabled."
        % scons_raw_version
    )
else:
    env.Tool("compilation_db")
    env.Alias("compiledb", env.CompilationDatabase("../compile_commands.json"))

addon_base_dir = "build/addons/ffmpeg/"
addon_platform_dir = addon_base_dir

if env["platform"] == "windows":
    addon_platform_dir += "win64/"
else:
    addon_platform_dir += "linux64/"

opts = Variables([], ARGUMENTS)
opts.Add(BoolVariable("verbose", "Enable verbose output for the compilation", False))
opts.Add(("ffmpeg_path", "Path to FFmpeg", ""))

opts.Update(env)

if not "ffmpeg_path" in env or not env["ffmpeg_path"]:
    raise RuntimeError("ffmpeg path not found")

if not env["verbose"]:
    methods.no_verbose(sys, env)

env.Append(CPPDEFINES=["GDEXTENSION"])
env.Append(CPPPATH=["../"])
sources = Glob("../*.cpp")
sources.extend(Glob("*.cpp"))
ffmpeg_path = env["ffmpeg_path"]

ffmpeg_libs = ["avcodec", "avfilter", "avformat", "avutil", "swresample", "swscale"]

env.Append(LIBS=ffmpeg_libs)
env.Prepend(CPPPATH=f"{ffmpeg_path}/include")
env.Append(LIBPATH=[f"{ffmpeg_path}/lib"])

if env["platform"] == "windows" and sys.platform == "win32" and not env.get("is_msvc", False):
    # Workaround for https://github.com/godotengine/godot-cpp/issues/1064
    env.Append(CCFLAGS=["-O3", "-Wwrite-strings"])
    env.Append(
        LINKFLAGS=[
            "-static",
            "-Wl,--no-undefined",
            "-static-libgcc",
            "-static-libstdc++",
        ]
    )
if env["platform"] == "macos":
    methods.write_macos_plist(
        f'{addon_platform_dir}libgdffmpeg.macos.{env["target"]}.framework',
        f'libgdffmpeg_advanced.macos.{env["target"]}',
        "org.godotengine.ffmpeg",
        "FFmpeg for Godot",
    )
    library = env.SharedLibrary(
        f'{addon_platform_dir}bin/libgdffmpeg.macos.{env["target"]}.framework/libgdffmpeg_advanced.macos.{env["target"]}',
        source=sources,
    )
else:
    library = env.SharedLibrary(
        f'{addon_platform_dir}libgdffmpeg{env["suffix"]}{env["SHLIBSUFFIX"]}',
        source=sources,
    )

ffmpeg_install_action = ffmpeg_download.ffmpeg_install(env, f"#{addon_platform_dir}", ffmpeg_path)
env.Depends(sources, env.Install(addon_base_dir, "ffmpeg.gdextension"))
env.Depends(sources, ffmpeg_install_action)

env.GLSL_HEADER("../yuv_to_rgb.glsl")
env.Depends(Glob("../*.glsl.gen.h"), ["#glsl_builders.py"])

Default(library)


def print_elapsed_time():
    elapsed_time_sec = round(time.time() - time_at_start, 3)
    time_ms = round((elapsed_time_sec % 1) * 1000)
    print("[Time elapsed: {}.{:03}]".format(time.strftime("%H:%M:%S", time.gmtime(elapsed_time_sec)), time_ms))


atexit.register(print_elapsed_time)
